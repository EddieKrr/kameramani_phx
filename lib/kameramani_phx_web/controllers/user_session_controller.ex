defmodule KameramaniPhxWeb.UserSessionController do
  use KameramaniPhxWeb, :controller

  alias KameramaniPhx.Accounts
  alias KameramaniPhxWeb.UserAuth

  @doc """
  Handles the GET /users/log-in route by redirecting to the custom auth page.
  """
  def new(conn, _params) do
    redirect(conn, to: ~p"/auth")
  end

  def create(conn, %{"_action" => "confirmed"} = params) do
    create(conn, params, "User confirmed successfully.")
  end

  def create(conn, params) do
    create(conn, params, "Welcome back!")
  end

  # magic link login
  defp create(conn, %{"user" => %{"token" => token} = user_params}, info) do
    case Accounts.login_user_by_magic_link(token) do
      {:ok, {user, tokens_to_disconnect}} ->
        UserAuth.disconnect_sessions(tokens_to_disconnect)

        conn
        |> put_flash(:info, info)
        |> UserAuth.log_in_user(user, user_params)

      _ ->
        conn
        |> put_flash(:error, "The link is invalid or it has expired.")
        |> redirect(to: ~p"/auth")
    end
  end

  # email + password login
  defp create(conn, %{"user" => user_params}, info) do
    %{"email" => email, "password" => password} = user_params

    if user = Accounts.get_user_by_email_and_password(email, password) do
      conn
      |> put_flash(:info, info)
      |> UserAuth.log_in_user(user, user_params)
    else
      conn
      |> put_flash(:error, "Invalid email or password")
      |> put_flash(:email, String.slice(email, 0, 160))
      |> redirect(to: ~p"/auth")
    end
  end

  def update_password(conn, %{"user" => user_params} = params) do
    user = conn.assigns.current_user.user
    if Accounts.sudo_mode?(user, -20) do
      case Accounts.update_user_password(user, user_params) do
        {:ok, {_user, expired_tokens}} ->
          UserAuth.disconnect_sessions(expired_tokens)

          conn
          |> put_session(:user_return_to, ~p"/users/settings")
          |> create(params, "Password updated successfully!")

        {:error, _changeset} ->
          conn
          |> put_flash(:error, "Failed to update password. Please check the requirements.")
          |> redirect(to: ~p"/users/settings")
      end
    else
      conn
      |> put_flash(:error, "You must re-authenticate to change your password.")
      |> redirect(to: ~p"/users/settings")
    end
  end

  def delete(conn, _params) do
    conn
    |> put_flash(:info, "Logged out successfully.")
    |> UserAuth.log_out_user()
  end
end
